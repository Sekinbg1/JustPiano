apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30
    useLibrary 'org.apache.http.legacy'
    ndkVersion "25.0.8775105"

    defaultConfig {
        applicationId "ly.pp.justpiano3"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 48
        versionName "4.6"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias 'as2134u'
            keyPassword 'as2134.@u'
            storeFile file('../key.jks')
            storePassword 'as2134.@u'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.config
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            proto {
                srcDir 'src/main/protobuf'
                include '**/*.proto'
            }
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'javazoom:jlayer:1.0.1'
    implementation 'com.king.anetty:anetty:1.0.3'
    implementation 'com.google.protobuf:protobuf-java:3.19.4'
    implementation 'com.android.support:multidex:1.0.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.4'
    }

    // 默认路径为 build/generated/source/proto
    // 该目录下会按 buildType 生成 debug / release 目录

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = "../"
                }
            }
        }
    }
}
